#lang at-exp s-exp rilouw-website/database/article-lang

@article["The programming language of your dreams"
         @pubdate[2018 6 26]
         '(english technology programming racket lisp)

@p{For a long time, my favorite language was Python. It's fairly well designed
   and useful for any task, from web servers like Django to 3D software like
   Blender; from neural networks like TensorFlow to cloud computing platforms
   like OpenStack.}

@p{Python offers so much libraries and tools for a developer to play with that
   you feel you can achieve anything, given the right tool.}

@p{What I really enjoyed above all was the community, and the feeling that
   everyone was according to the same set of standards (it was of course not
   always the case) and that you could somehow easily agree on what was
   "Pythonic" and what was not (spoiler alert: in fact, you cannot).}

@p{Now that I shifted to using lisp languages (especially Racket), I see my
   past self as childish and primitive, but it has been a brease passing through
   Python as a part of my road to becoming a better developer, and alas I may
   never use Python again for personal projects, I have learned a lot and would
   still recommend it for anyone wanting to achieve efficiently, quickly and
   elegantly some IT project.}

@p{So what is so good in Racket, that it made me think I finally found the
   language of my dreams?}

@p{Well, there's a lot of features that make Racket an awesome language to work
   with: parameters, continuations, contracts, syntax-parse...
   But the feature I want to write about today is by far the one I find really
   transcendental: @inline-code{#lang} (pronounce "hash-lang").}

@p{@inline-code{#lang} allows writing your own languages. Let's dive into it!}

@(fold)

@section["The myth of the universal translator"

@p{Sometimes comes a task in your daily job that looks like there's something
   deeper going on.}

@p{Sometimes, you feel like your team would really benefit from a tool that
   could understand some simple data (XML, JSON, a database, whatever...) and
   execute some generic behavior depending on a configuration you can tweak.}

@p{It' easy, you say: I understand my job and my colleagues' job well, I'm sure
   I can find a common ground from which I could define generic behaviors, and
   then customize them with parameters.}

@p{Let's say you work in a game development studio. You have multiple teams
   (gameplay, engine, UI, network, 2D & 3D artists...etc) working together on a
   final product. You usually want to see what each of those teams are working
   on, preferably merged together in a bundle that would look as close as
   possible to what the final result should be.}

@p{To achieve that, you need build tools, communication tools, reports, tests,
   bundlers, a whole suite of custom software and I can certainly assert you
   have a lot of data files in different formats.}

@p{You say: I need a tool that would understand the needs of those teams,
   and translate them into computer logic so that everything can be compiled
   together.}

@p{I need a custom file format. It needs to be simple, it needs to be adapted
   to the vocabulary of the teams.}

@p{In fact, you could say that you need a kind of "universal translator".
   Not a translator for natural languages, say French, English, Japanese...
   But a translator for your teams' languages:
   Gameplay-language, Artist-language, UI-language.}

@p{You need to start somewhere. So let's say you design what the UI-language
   could be:}

@code["" "XML"]{
  <ui>
    <menu id="title_menu">
      <button goto="campaign_menu">Campaign</button>
      <button goto="multiplayer_menu">Multiplayer</button>
      <button goto="credits_menu">Credits</button>
    </menu>
  </ui>
}

@p{Then design a way to make your software behave depending on that file.}

@p{If you're using a typed language like C# or Java,
   you're likely to make a @inline-code{UI} class, a @inline-code{Menu} class,
   a @inline-code{Button} class...etc}

@p{Soon you'll start to realize that the more you add classes, the more complexe
   your software becomes, and more bloated the scope reveals itself.}

@p{For example: One of your UI designers needs a way to declare variables for
   the size of elements, so that some of them can be of a relative size compared
   to other elements. You start putting references to objects everywhere
   and end up with a bowl of spaghetti code.}

@p{In fact, the more you work on it, the more you are "mapping" software to a
   human craft, the less you see the end of the tunnel.
   Obviously because there is no proper end to begin with.}

@p{It may take years until you accept the sad truth that what you're trying to
   do is way out of your scope. You're making a software that permits
   non-technical users to express their specific needs in the vocabulary of
   their specific domain, while keeping everything together and doing the
   Right Thing™ every time, preferably in an automated fashion.}

@p{You are making the @strong{Universal Translator}. The ultimate software
   that allows non-developers to develop. The ultimate machine that understands
   humans. Microsoft tried it. Adobe tried it. Pretty much every big IT company
   tried to make the Universal Translator. All failed.}

@p{The bad news is that most programming languages are not suited for
   programming that kind of software. Because actually, that's not a software
   you want to make, but a @strong{language}.}

@p{The good news is that there is a name for such a language.
   It's called a DSL: Domain Specific Language.}

@p{DSLs are not a Universal Translator. They are just a way of expressing a
   domain using the most adapted language.}

@p{You will not provide your team software that handles programming for them
   (we saw this is impossible), you will provide them a language so that they
   can program themselves, in the language that fits the best how they express
   their job.}

@p{Luckily, there is a family of programming languages that have been
   specifically designed to create DSLs. It's called @'|lisp|.}

@p{In the lisp family, there are a lot of different languages. Some designed
   for science, some designed for learning computer science, some general
   purposed, and some "language oriented".}

@p{Racket is a language oriented language. It's my favorite lisp, and if you
   followed until now, you're now ready to discover some of the tricks that make
   this language incredibly powerful in creating domain specific languages.}

]

@section["Common Lisp vs Scheme"

@p{Racket is an implementation of Scheme, which is one of the two major lisp
   families (the other one being Common Lisp).
   Both have been around for more than 60 years (you read that well) and mostly
   differ from each other in deep philosophical questions about whether or not
   we can implicitely introduce variables in scope without telling the user.
   This is known as "breaking hygiene" in Scheme, or as "anaphoric awesomeness"
   in Common Lisp (note that the vocabulary difference says it all about
   what each community think of this practice).}

@p{Scheme says no, you can't break hygiene (except maybe sometimes but
   don't tell my mom), Common Lisp says yes, of course you can, that's an
   awesome feature (it is). That's about it. There are some other differences
   but really, unless you are deeply interested in lisp design,
   you don't care (for now).}

@p{Racket says: With great power comes great responsibility. I'm Scheme-based,
   so let's not break hygiene by default. But let's allow breaking hygiene
   step by step, by building a framework that prevents things from getting
   hairy, but still unlock the awesomeness of anaphoric macros.}

@p{If I'm writing about Racket today, that's (among other reasons) because
   I think that's the right approach. The only drawback is that it makes
   learning Racket macros a bit difficult for beginners. But in the long time,
   it really pays off.}

@p{The only thing you need to remember about this for now is that Racket
   is a Scheme, and that Scheme is a lisp:}

@'(table
    (tr
      (th ([colspan "2"] [class "center"]) "Lisp family"))
    (tr
      (th ([class "center width-50"]) "Common Lisp")
      (th ([class "center width-50"]) "Scheme"))
    (tr
      (td
        (div "CLisp")
        (div "CMU Common Lisp")
        (div "GNU Common Lisp")
        (div "Steel Bank Common Lisp")
        (div "..."))
      (td
        (div (strong "Racket ←"))
        (div "Chez Scheme")
        (div "GNU Guile")
        (div "Chicken Scheme")
        (div "..."))))

]

@section["Lists and atoms"

@p{Coming back to what lisp looks like. There is no exact definition of what a
   lisp is or is not, but you can usually recognize them at their famous
   @inline-code{((((parentheses))))}.}

@p{The format in which one writes lisp is called "s-expressions" or "s-exp"
   and is the basic skeleton from which you write a program. It's a list of
   lists of lists of lists...etc}

@p{It looks like this:}

@code["" "lisp"]{
  (always (walk (muddy-forest (pine-trees)))
    (let mushroom (fetch (leafy-forest-ground))
      (when (comestible? mushroom)
        (put mushroom -> tiny-basket))))
}

@p{You can nearly smell the spicy scent of pines in that piece of code.
   Half code, half poem, I'd dare say.}

@p{A list is a pair of parentheses @inline-code{()}
   (square brackets @inline-code{[]} and curly braces @inline-code{{}} are
   also accepted) and everything that is not blank spaces nor parentheses is
   called an "atom": @inline-code{always}, @inline-code{comestible?},
   @inline-code{->}, @inline-code{forest}, @inline-code{basket},
   @inline-code{mushroom} are all atoms.}

@p{Atoms can contain any characters in Racket, even symbols like
   @inline-code{%}, @inline-code{_}, @inline-code{-}, @inline-code{+},
   @inline-code{~}, @inline-code{&}, @inline-code{@"@"}, @inline-code{=},
   whatever you like.}

@p{This is because lists and atoms are the @strong{only} piece of syntax
   hardcoded in the language. Everything else is defined by programmers.
   The @inline-code{if} clause, variable definitions, classes if you want
   classes, structs if you want structs...}

@p{The language can be object oriented if you want. Functional if you want.
   It can be your own language. You are the one defining the rules and the
   syntax.}

@p{This is why the first example I gave is written in a language which is not
   Racket, nor Scheme, nor Common Lisp. It's just one kind of lisp I just
   made up to best express my desire of fetching mushrooms in the forest.}

@p{This lack of formal syntax allows two things:}

@dotted-list[
  "The language can be whatever you like"
  "The language can parse itself as data"]

@p{The second point is the most important thing to understand. Instead of
   using some kind of file format for your data, you can actually write your
   data as code. Because there is no fundamental difference between a piece of
   code like @inline-code{(send love letters)} and a listing of data like
   @inline-code{(banana apple pear)}. Both are lists holding three atoms.
   As a programmer, you are the one defining their meaning.}

@p{Therefore, you can write JSON as lisp, HTML as lisp,
   even Javascript as lisp:}

@code["" "JSON-lisp"]{
  (users [
    (
      (name "Jane")
      (surname "Doe")
      (inventory [
        ((name "Life potion") (effect "HP_INC") (amount 10))
      ])
    )
    (
      (name "Hanako")
      (surname "Yamada")
      (inventory [
        ((name "Magic staff") (type "weapon") (damage 4))
      ])
    )
  ])
}

@p{NB: I indented it like JSON for the sake of the example, but usually lisp
   programmers care about their parentheses and don't like leaving them
   alone on their lines.}

@code["" "HTML-lisp"]{
  (html ([lang "en"])
    (head
      (title "My awesome blog about ponies"))
    (body
      (article ([class "first"])
        (p "I like all ponies.")
        (p "But my favorite one is: ")
        (a ([href "/favorite"]
            [class "rainbow-link"])
          "Click here to discover my favorite pony!!"))))
}

@p{The blog you are reading right now is actually written in this kind
   of HTML-lisp language.}

@code["percentage.js" "Javascript-lisp"]{
  (function print_percentage (a b)
    (var result null)
    (try
     (set result (* (/ a b) 100))
     (catch (e)
       (console.log "Error: {}" e)
       (return false)))
    (console.log "Percentage is {}%" result)
    (return true))
}

@p{My point is: you do whatever you like with lisp, and give it the shape you
   like, regardless of the type of data/code you are working with.
   This makes it the perfect tool for glueing together different
   technologies, abstracting away different implementations or formats, or
   designing new languages for your teams.}

@p{For the more savvy of you, if you want the exact technical term that
   refers to this property of lisp to be at the same time code and data:
   it's called @link["Homoiconicity"
                     "https://en.wikipedia.org/wiki/Homoiconicity"].}

@p{I can already hear you ask: But what is the point? Javascript is good as it
   is, I don't need this weird parentheses-everywhere syntax!}

@p{Well, the fact that this javascript code is also @strong{data} means you
   can edit and modify it on-the-fly. You can create new kinds of control
   words like @inline-code{do..until}, a truly working @inline-code{for-each},
   some kind of integrated SQL query language
   @inline-code{(select * from users)} that would work on JSON-lisp.
   You name it.}

@p{You can do reflection on your own code:}

@console["Sample console session"]{
  > (define code (read "percentage.js"))
  > (first code)
  'function

  > (second code)
  'print_percentage

  > (find 'console.log code)
  '((console.log "Error: {}" e)
    (console.log "Percentage is {}%" result))
}]

@p{All your code is editable as a list of atoms. It is normally very difficult
   to parse and modify Javascript because of its syntax (programmers of
   linters, minifiers or transpilers can testify), but with s-expressions,
   everything becomes a lot simpler.}

@section["Making languages with Racket"

@p{Remember the XML UI-language we wrote earlier so that our UI team can
   design interfaces? In s-exp, it would look like this:}

@code["title-screen.ui" "ui-lang"]{
  (ui
    (menu ([id "title-menu"])
      (button ([goto "campaign-menu"]) "Campaign")
      (button ([goto "multiplayer-menu"]) "Multiplayer")
      (button ([goto "credits-menu"]) "Credits")))
}

@p{Let's write a Racket module that can parse and display that!}

@p{The first thing to do is to write a new Racket module with the name of our
   language: @inline-code{ui-lang.rkt}.}

@p{The bare minimum for a language in Racket looks like this:}

@code["ui-lang.rkt" "Racket"]{
  #lang racket/base
  (provide
    (except-out (all-from-out racket/base) #%module-begin)
    (rename-out [module-begin #%module-begin]))

  (define-syntax-rule (module-begin expr ...)
    (#%module-begin expr ...))
}

@p{It's a bit intimidating the first time you see it, but it's actually really
   simple once you understand that everything shown here is just a smart
   combination of basic Racket features.}

@p{Let's take it line by line:}

@code["Line 1" ""]{
  #lang racket/base
}

@p{This first line means @inline-code{racket/base} is the language we are using
   in the module. Racket provides a lot of different languages. For what we want
   to do (make a language), the base racket library is enough.}

@p{In Racket, a module must always start with a @inline-code{#lang} clause, so
   that your file is recognized as a Racket module, and so that the Racket
   compiler can know in which language it is written.}

@code["Line 2" ""]{
  (provide ...)
}

@p{The @inline-code{provide} clause is the way for modules to export their
   functions. @inline-code{provide} takes a list of identifiers defined in
   the module (or imported) and exposes them as a public interface, so that when
   a programmer requires your module with
   @inline-code{(require "my-module.rkt")}, they get the public functions,
   structs and classes your module implements.}

@code["Lines 2 to 4" ""]{
  (provide
    (except-out (all-from-out racket/base) #%module-begin)
    (rename-out [module-begin #%module-begin]))
}

@p{This clause is the magic that makes your file not a simple Racket module, but
   a language.}

@p{It tells your module to provide all identifiers from
   @inline-code{racket/base} with @inline-code{(all-from-out racket/base)} but
   @inline-code{except-out} the @inline-code{#%module-begin} function.}

@p{In fact, @inline-code{#%module-begin} is not really a function, it's a
   syntax. It's the main wrapper that goes around a Racket module when Racket
   interprets it. This is a bit like the @inline-code{__init__.py} file that
   gets executed when you import a Python module.}

@p{What we are doing here when we say @inline-code{except-out #%module-begin}
   is that we disable the default Racket behavior to replace it with our own.}

@p{You can see that replacement happening on line 4:
   @inline-code{(rename-out [module-begin #%module-begin])}. We rename our own
   @inline-code{module-begin} syntax to be the new @inline-code{#%module-begin}.}

@code["Lines 6 and 7" ""]{
  (define-syntax-rule (module-begin expr ...)
    (#%module-begin expr ...))
}

@p{This last piece of code is the definition of our own
   @inline-code{module-begin} syntax.}

@p{Right now it does not do much. It accepts an undefined number of expressions
   @inline-code{expr ...} and pass them unchanged to the genuine
   @inline-code{#%module-begin} of Racket (the one we import from
   @inline-code{racket/base} but don't provide back).}

@p{We successfully created a language that does nothing but read the content
   of our file and interpret it as Racket. Which will obviously fail because
   most of our file has undefined elements: @inline-code{ui},
   @inline-code{button}...etc}

@p{But there is something happening nonetheless that is interesting:
   We did not in any ways had to write parsing logic. We just reused the
   reader from Racket.}

@p{That's where things start to get interesting. We get the full power of Racket
   at our disposal to implement a software that will manipulate the code,
   without having to care about parsing it. All the hard work of reading streams
   of characters, tokenize them, put them in data structures... has been done
   for us.}

@p{All we have to do is write the logic of what the language is supposed to do.}

@p{The last step before we can try this is to actually use that language in our
   ui file. We need to add a @inline-code{#lang} clause in our file:}

@code["title-screen.ui" "ui-lang"]{
  #lang s-exp "ui-lang.rkt"
  (ui
    (menu ([id "title-menu"])
      (button ([goto "campaign-menu"]) "Campaign")
      (button ([goto "multiplayer-menu"]) "Multiplayer")
      (button ([goto "credits-menu"]) "Credits")))
}

@p{@inline-code{s-exp} is the default s-expression reader from Racket. It takes
   a Racket module path as argument, reads the next lines as s-expressions, then
   pass them to @inline-code{#%module-begin} inside our lang module.}

@p{Let's try this now:}

@console["Racket REPL"]{
  > (require "title-screen.ui")
  ; title-screen.ui:2:1: ui: unbound identifier in module
  ;   in: ui
  ; [,bt for context]
}

@p{It works! (developers are the only kind of person who can shout happily when
   they see an error).}

@p{Of course, Racket complains that @inline-code{ui} does not exist.
   But this also means the file got read correctly!}

@p{Interestingly, if we tweak a bit our ui-lang module, we can get our file to
   render as a list by simply quoting it:}

@code["Lines 6 and 7" ""]{
  (define-syntax-rule (module-begin expr ...)
    (#%module-begin 'expr ...)) ;; notice the simple quote
}

@console["Racket REPL"]{
  > (require "title-screen.ui")
  '(ui
     (menu
      ((id "title-menu"))
      (button ((goto "campaign-menu")) "Campaign")
      (button ((goto "multiplayer-menu")) "Multiplayer")
      (button ((goto "credits-menu")) "Credits")))
}

]

@section["Notes for second article"

@code["title-screen.ui" "ui"]{
  title-menu:
    [Campaign] => campaign-menu
    [Multiplayer] => multiplayer-menu
    [Credits] => credits-menu
}

]

]
